/* $Id: ShExParser.ypp,v 1.2 2008-10-24 10:57:32 eric Exp $ -*- mode: c++ -*- */
/** \file ShExParser.yy Contains the Bison parser source */

/*** yacc/bison Declarations ***/

/* Require bison 2.3 or later */
%require "2.3"

/* add debug output code to generated parser. disable this for release
 * versions. */
%debug

/* start symbol is named "start" */
%start ShExDoc

/* write out a header file containing the token defines */
%defines

/* use newer C++ skeleton file */
%skeleton "lalr1.cc"

/* namespace to enclose parser in */
%name-prefix="w3c_sw"

/* set the parser's class identifier */
%define "parser_class_name" "ShExParser"

/* keep track of the current position within the input */
%locations
%initial-action
{
    // initialize the initial location object
    @$.begin.filename = @$.end.filename = &driver.streamname;
    driver.yylloc = &@$;
};

/* The driver is passed by reference to the parser and to the scanner. This
 * provides a simple but effective pure interface, not relying on global
 * variables. */
%parse-param { class ShExDriver& driver }

/* verbose error messages */
%error-verbose

// %{ //  ##bison1
%code requires { // ##bison2
/* Bison seems to test inclusion with PARSER_HEADER_H, rather than something
 * which varies by parser_class_name . Overriding with define specific to
 * this parser.
 */
#endif /* !PARSER_HEADER_H */
#ifndef SWSEX_PARSER_HPP
#define SWSEX_PARSER_HPP

#include "SWObjects.hpp"
#include "RdfDB.hpp"
#include "ParserCommon.hpp"
#include "ShExSchema.hpp"
#include <stack>

namespace w3c_sw {

class ShExDriver : public YaccDataDriver {
    location* yylloc; // can't move up into YaccDriver 'cause location.hh hasn't been included yet.
    friend class ShExParser;
protected:
    ShExSchema* curSchema;
    ShExSchema::SetRule* curSetRule;
    ShExSchema::AtomicRule::NameClass* curNameClass;
    ShExSchema::RulePattern* curRulePattern;
    std::vector<const TTerm*>* curTTerms;
    const TTerm* curSubject;
    const TTerm* curPredicate;
public:
    void setSchema (ShExSchema* schema) { curSchema = schema; }
    static size_t DefaultAbortErrorCount;

    ShExDriver (std::string baseURI, AtomFactory* atomFactory, size_t abortErrorCount = DefaultAbortErrorCount)
	: YaccDataDriver(baseURI, atomFactory, abortErrorCount)
    {  }

    ShExSchema* parse(IStreamContext& in);
    ShExSchema* parse(IStreamContext& in, ShExSchema* schema);
    ShExSchema* parse(std::string swsexStr, ShExSchema* schema);


    /** Pointer to the current lexer instance, this is used to connect the
     * parser to the scanner. It is used in the yylex macro. */
    class ShExScanner* lexer;

    const BNode* createBNode () {
	if (descriptiveBNodeLabels) {
	    std::stringstream ss;
	    ss << *(yylloc->begin.filename) <<":"<< yylloc->begin.line <<":"<< yylloc->begin.column;
	    return atomFactory->getBNode(ss.str(), &bnodeMap);
	} else {
	    return atomFactory->createBNode();
	}
    }
};

} // namespace w3c_sw

// %} // ##bison1
} // ##bison2

 /*** BEGIN ShEx language tokens. ***/

%union {
    struct {std::string* label; std::string* text;} p_CODE;
    int p_int;
    ShExSchema::AtomicRule::NameClass* p_nameClass;
    bool p_bool;
    ShExSchema::SetRule* p_setRule;
    ShExSchema::RulePattern* p_rulePattern;
    ShExSchema::RuleActions* p_ruleActions;
    ShExSchema::AtomicRule::Value* p_value;
    ShExSchema::AtomicRule::ValueSet* p_valueSet;
    struct {unsigned min; unsigned max;} p_RepeatRange;

    struct {const URI* uri; LANGTAG* langtag;} p_uri_or_langtag;

    /* Terminals */
    LANGTAG* p_LANGTAG;

    std::string* p_string;

    const NumericRDFLiteral* p_NumericRDFLiteral;
    ShExSchema::URIstem p_URIstem;
    std::vector<ShExSchema::URIstem>* p_URIstems;

    const TTerm* p_TTerm;
    const URI* p_URI;
    struct {const TTerm* subject; const TTerm* predicate;} p_SubjectPredicatePair;

}

%{
#include "ShExCScanner.hpp"
%}
%token			__EOF__	     0	"end of file"
/* START TokenBlock */
/* Terminals */

%token IT_start GT_EQUAL IT_BASE IT_PREFIX GT_LCURLEY GT_RCURLEY GT_PIPE GT_COMMA GT_DOLLAR GT_OPT GT_LPAREN GT_RPAREN GT_CARROT GT_DOT GT_AT IT_SPARQL_SRX GT_KINDA GT_SEMI GT_LBRACKET GT_RBRACKET GT_MINUS GT_TIMES GT_PLUS GT_DTYPE RDF_TYPE

%token <p_CODE> CODE
%token <p_TTerm> IT_true IT_false BLANK_NODE_LABEL ANON
%token <p_NumericRDFLiteral> INTEGER DECIMAL DOUBLE
// deepest typing for these tokens:
// %token <p_BooleanRDFLiteral> IT_true IT_false
// %token <p_NumericRDFLiteral> INTEGER DECIMAL DOUBLE
// %token <p_BNode> BLANK_NODE_LABEL ANON
%token <p_string> STRING_LITERAL1 STRING_LITERAL2 STRING_LITERAL_LONG1 STRING_LITERAL_LONG2
%token <p_URI> PNAME_NS PNAME_LN IRIREF
%token <p_LANGTAG> LANGTAG

 // !!NEW tokens
%type <p_TTerm> _O_Qlabel_E_Or_QtypeSpec_E_S_QCODE_E_Star_C label object collection RDFLiteral literal NumericLiteral BooleanLiteral BlankNode blankNodePropertyList verb predicate _Qobject_E_Star _Qdefahlt_E_Opt defahlt _O_Qiri_E_Or_Qliteral_E_C valueChoice
 //%type <p_TTerms> _Qvalue_E_Plus valueSet
%type <p_int> _QINTEGER_E_Opt _O_QGT_COMMA_E_S_QINTEGER_E_Opt_C _Q_O_QGT_COMMA_E_S_QINTEGER_E_Opt_C_E_Opt
%type <p_nameClass> nameClass
%type <p_bool> _QGT_CARROT_E_Opt _QGT_OPT_E_Opt _QGT_KINDA_E_Opt
 //%type <p_bool> _QGT_NOT_E_Opt
%type <p_rulePattern> typeSpec OrExpression _O_QGT_PIPE_E_S_QAndExpression_E_C AndExpression _O_QGT_COMMA_E_S_QUnaryExpression_E_C UnaryExpression _O_Qarc_E_Or_QGT_LPAREN_E_S_QOrExpression_E_S_QGT_RPAREN_E_S_QGT_OPT_E_Opt_S_QCODE_E_Star_C arc
%type <p_ruleActions> _QCODE_E_Star
%type <p_value> valueSpec
%type <p_RepeatRange> _QrepeatCount_E_Opt repeatCount
%type <p_URIstems> exclusions _Q_O_QGT_MINUS_E_S_QiriStem_E_C_E_Star
%type <p_URIstem> iriStem _O_QGT_MINUS_E_S_QiriStem_E_C

%type <p_URI> iri PrefixedName
// deepest typing for these productions:
// %type <p_RDFLiteral> RDFLiteral literal
// %type <p_NumericRDFLiteral> NumericLiteral
// %type <p_BooleanRDFLiteral> BooleanLiteral
%type <p_string> String
%type <p_uri_or_langtag> _O_QLANGTAG_E_Or_QGT_DTYPE_E_S_Qiri_E_C
                       _Q_O_QLANGTAG_E_Or_QGT_DTYPE_E_S_Qiri_E_C_E_Opt

/* END TokenBlock */

//%destructor { delete $$; } BlankNode

 /*** END ShEx language tokens. ***/

%{
#include "ShExCScanner.hpp"

/* this "connects" the bison parser in the driver to the flex scanner class
 * object. it defines the yylex() function call to pull the next token from the
 * current lexer object of the driver context. */
#undef yylex
#define yylex driver.lexer->lexWrapper
%}

%% /*** Grammar Rules ***/

 /*** BEGIN ShEx language structure. ***/
ShExDoc:
    // statement*
    _Qstatement_E_Star	
;

_Qstatement_E_Star:
    
  | _Qstatement_E_Star statement	
;

statement:
    directive	
  | shape	
;

directive:
    BaseDecl	
  | PrefixDecl	
  | IT_start GT_EQUAL _O_Qlabel_E_Or_QtypeSpec_E_S_QCODE_E_Star_C	{
	driver.curSchema->start = $3;
    }
;

_QCODE_E_Star:
    {
	$$ = NULL;
    }
  | _QCODE_E_Star CODE	{
	$$ = ($1 == NULL)
	    ? new ShExSchema::RuleActions(driver.curRulePattern)
	    : $1;
	$$->codeMap.insert(std::make_pair(*$2.label, *$2.text));
	delete $2.label;
	delete $2.text;
    }
;

_O_Qlabel_E_Or_QtypeSpec_E_S_QCODE_E_Star_C:
    label	
  | typeSpec {
	driver.curRulePattern = $1;
      } _QCODE_E_Star	{
	  driver.curRulePattern = NULL;
	  $$ = driver.createBNode();
	  driver.curSchema->ruleMap.insert(std::make_pair($$, $3 == NULL ? $1 : $3));
      }
;

BaseDecl:
    IT_BASE IRIREF	{
	driver.setBase($2->getLexicalValue());
    }
;

PrefixDecl:
    IT_PREFIX {
	driver.ignorePrefix(true);
      } PNAME_NS {
	  driver.ignorePrefix(false);
      } IRIREF	{
	  std::string prefix($3->getLexicalValue());
	  driver.addPrefix(prefix.substr(0, prefix.length()-1), $5);
      }
;

shape:
    label typeSpec {
	driver.curRulePattern = $2;
      } _QCODE_E_Star	{
	  driver.curRulePattern = NULL;
	  driver.curSchema->ruleMap.insert(std::make_pair($1, $4 == NULL ? $2 : $4));
      }
;

typeSpec:
    GT_LCURLEY {
	$<p_nameClass>$ = driver.curNameClass;
	driver.curNameClass = NULL;
      } {
	  $<p_rulePattern>$ = driver.curRulePattern;
	  driver.curRulePattern = NULL;
      } OrExpression GT_RCURLEY	{
	  driver.curRulePattern = $<p_rulePattern>3;
	  driver.curNameClass = $<p_nameClass>2;
	  $$ = $4;
      }
;

OrExpression:
    {
	$<p_setRule>$ = driver.curSetRule;
	driver.curSetRule = NULL;
    } AndExpression _Q_O_QGT_PIPE_E_S_QAndExpression_E_C_E_Star	{
	if (driver.curSetRule == NULL)
	    $$ = $2;
	else {
	    driver.curSetRule->rules.insert(driver.curSetRule->rules.begin(), $2);
	    $$ = driver.curSetRule;
	}
	driver.curSetRule = $<p_setRule>1;
    }
;

_O_QGT_PIPE_E_S_QAndExpression_E_C:
    GT_PIPE AndExpression	{
	$$ = $2;
    }
;

_Q_O_QGT_PIPE_E_S_QAndExpression_E_C_E_Star:
    
  | _Q_O_QGT_PIPE_E_S_QAndExpression_E_C_E_Star _O_QGT_PIPE_E_S_QAndExpression_E_C	{
	if (driver.curSetRule == NULL)
	    driver.curSetRule = new ShExSchema::OrRule();
	driver.curSetRule->rules.push_back($2);
    }
;

AndExpression:
    {
	$<p_setRule>$ = driver.curSetRule;
	driver.curSetRule = NULL;
    } UnaryExpression _Q_O_QGT_COMMA_E_S_QUnaryExpression_E_C_E_Star	{
	if (driver.curSetRule == NULL)
	    $$ = $2;
	else {
	    driver.curSetRule->rules.insert(driver.curSetRule->rules.begin(), $2);
	    $$ = driver.curSetRule;
	}
	driver.curSetRule = $<p_setRule>1;
    }
;

_O_QGT_COMMA_E_S_QUnaryExpression_E_C:
    GT_COMMA UnaryExpression	{
	$$ = $2;
    }
;

_Q_O_QGT_COMMA_E_S_QUnaryExpression_E_C_E_Star:
    
  | _Q_O_QGT_COMMA_E_S_QUnaryExpression_E_C_E_Star _O_QGT_COMMA_E_S_QUnaryExpression_E_C	{
	if (driver.curSetRule == NULL)
	    driver.curSetRule = new ShExSchema::AndRule();
	driver.curSetRule->rules.push_back($2);
    }
;

UnaryExpression:
    _Q_O_QGT_DOLLAR_E_S_Qiri_E_C_E_Opt _O_Qarc_E_Or_QGT_LPAREN_E_S_QOrExpression_E_S_QGT_RPAREN_E_S_QGT_OPT_E_Opt_S_QCODE_E_Star_C	{
	// $$ = $1 ? new ShExSchema::NegatedRule($2) : $2;
	// @@ $1 is a label
	$$ = $2;
    }
;

// @@vv
_O_QGT_DOLLAR_E_S_Qiri_E_C:
    GT_DOLLAR iri	
;

_Q_O_QGT_DOLLAR_E_S_Qiri_E_C_E_Opt:
    
  | _O_QGT_DOLLAR_E_S_Qiri_E_C	
;

_QGT_OPT_E_Opt:
    {
	$$ = false;
    }
  | GT_OPT	{
        $$ = true;
    }
;

// @@^^

_O_Qarc_E_Or_QGT_LPAREN_E_S_QOrExpression_E_S_QGT_RPAREN_E_S_QGT_OPT_E_Opt_S_QCODE_E_Star_C:
    arc	
  | GT_LPAREN OrExpression GT_RPAREN {
	driver.curRulePattern = $2;
      } _QGT_OPT_E_Opt _QCODE_E_Star	{
	  // @@ set flag from $5
	  driver.curRulePattern = NULL;
	  $$ = $6 == NULL ? $2 : $6;
      }
;

label:
    iri	{
	$$ = $1;
    }
  | BlankNode	{
	$$ = $1;
    }
;

arc:
    _QGT_CARROT_E_Opt nameClass {
	// pass down the nameClass 'cause we need valueSpec's CODE* to be able
	// to make a AtomicRule and potentially wrap it with RuleActions.
	driver.curNameClass = $2;
	$2->reverse = $1;
      } valueSpec _Qdefahlt_E_Opt _QrepeatCount_E_Opt {
	  if ($5)
	      w3c_sw_NEED_IMPL("ShEx parser support for default");
	  driver.curRulePattern = new ShExSchema::AtomicRule(driver.curNameClass, $4, $6.min, $6.max);
      } _QblankNodePropertyList_E_Opt _QCODE_E_Star	{
	  $$ = $9 == NULL ? driver.curRulePattern : $9;
	  driver.curRulePattern = NULL;
	  driver.curNameClass = NULL;
	}
;

/*_QGT_NOT_E_Opt:
    {
	$$ = false;
    }
  | GT_NOT	{
      $$ = true;
    }
;*/

_QGT_CARROT_E_Opt:
    {
	$$ = false;
    }
  | GT_CARROT	{
	$$ = true;
    }
;

_Qdefahlt_E_Opt:
    {
	$$ = NULL;
    }
  | defahlt	
;

_QrepeatCount_E_Opt:
    {
	$$.min = 1;
	$$.max = 1;
    }
  | repeatCount	
;

// @@vv
_QblankNodePropertyList_E_Opt:
    
  | blankNodePropertyList	
;
// @@^^

nameClass:
    iriStem	{
	if ($1.opt)
	    $$ = new ShExSchema::AtomicRule::NamePattern($1.uri);
	else
	    $$ = new ShExSchema::AtomicRule::NameTerm($1.uri);
    }
  | GT_DOT exclusions	{
	$$ = new ShExSchema::AtomicRule::NameAll($2);
    }
  | RDF_TYPE	{
	$$ = new ShExSchema::AtomicRule::NameTerm(TTerm::RDF_type);
    }
;

valueSpec:
    GT_AT label	{
	$$ = new ShExSchema::AtomicRule::ValueReference($2);
    }
  | typeSpec	{
	const TTerm* b = driver.createBNode();
	driver.curSchema->ruleMap.insert(std::make_pair(b, $1));
	$$ = new ShExSchema::AtomicRule::ValueReference(b);
    }
  | iri	{
	$$ = new ShExSchema::AtomicRule::ValueType($1);
    }
  | {
	ShExSchema::AtomicRule::ValueSet* t = new ShExSchema::AtomicRule::ValueSet();
	$<p_valueSet>$  = t;
	driver.curTTerms = &t->tterms;
      } valueSet	{
	  $$ = $<p_valueSet>1;
      }
  | GT_DOT exclusions	{
	$$ = new ShExSchema::AtomicRule::ValueAny($2);
    }
  | IT_SPARQL_SRX iri	{
        w3c_sw_NEED_IMPL("SPARQL Results Format callout");
    }
;

iriStem:
    iri _QGT_KINDA_E_Opt	{
	$$.uri = $1;
	$$.opt = $2;
    }
;

_QGT_KINDA_E_Opt:
    {
	$$ = false;
    }
  | GT_KINDA	{
        $$ = true;
    }
;

defahlt:
    GT_EQUAL _O_Qiri_E_Or_Qliteral_E_C	{
	$$ = $2;
    }
;

_O_Qiri_E_Or_Qliteral_E_C:
    iri	{
	$$ = $1;
    }
  | literal	
;

//@@vv
predicateObjectList:
    // verb objectList ( ';' ( verb objectList )? )*
    verb objectList _Q_O_QGT_SEMI_E_S_Qverb_E_S_QobjectList_E_Opt_C_E_Star	
;

// verb objectList
_O_Qverb_E_S_QobjectList_E_C:
    verb objectList	
;

// ( verb objectList )?
_Q_O_Qverb_E_S_QobjectList_E_C_E_Opt:
    
  | _O_Qverb_E_S_QobjectList_E_C	
;

// ';' ( verb objectList )?
_O_QGT_SEMI_E_S_Qverb_E_S_QobjectList_E_Opt_C:
    GT_SEMI _Q_O_Qverb_E_S_QobjectList_E_C_E_Opt	
;

// ( ';' ( verb objectList )? )*
_Q_O_QGT_SEMI_E_S_Qverb_E_S_QobjectList_E_Opt_C_E_Star:
    
  | _Q_O_QGT_SEMI_E_S_Qverb_E_S_QobjectList_E_Opt_C_E_Star _O_QGT_SEMI_E_S_Qverb_E_S_QobjectList_E_Opt_C	
;

objectList:
    // object ( ',' object )*
    object _Q_O_QGT_COMMA_E_S_Qobject_E_C_E_Star	
;

// ',' object
_O_QGT_COMMA_E_S_Qobject_E_C:
    GT_COMMA object	
;

// ( ',' object )*
_Q_O_QGT_COMMA_E_S_Qobject_E_C_E_Star:
    
  | _Q_O_QGT_COMMA_E_S_Qobject_E_C_E_Star _O_QGT_COMMA_E_S_Qobject_E_C	
;

verb:
    predicate	{
	$$ = $1;
	driver.curPredicate = $$;
    }
  | RDF_TYPE	{
      $$ = TTerm::RDF_type;
	driver.curPredicate = $$;
    }
;

predicate:
    iri	{
	$$ = $1;
    }
;

object:
    iri	{
	driver.curBGP->addTriplePattern(driver.atomFactory->getTriple(driver.curSubject, driver.curPredicate, $1));
    }
  | BlankNode	{
	driver.curBGP->addTriplePattern(driver.atomFactory->getTriple(driver.curSubject, driver.curPredicate, $1));
    }
  | collection	{
	driver.curBGP->addTriplePattern(driver.atomFactory->getTriple(driver.curSubject, driver.curPredicate, $1));
    }
  | blankNodePropertyList	{
	driver.curBGP->addTriplePattern(driver.atomFactory->getTriple(driver.curSubject, driver.curPredicate, $1));
    }
  | literal	{
	driver.curBGP->addTriplePattern(driver.atomFactory->getTriple(driver.curSubject, driver.curPredicate, $1));
    }
;

literal:
    RDFLiteral	
  | NumericLiteral	// { $$ = $1; }
  | BooleanLiteral	// { $$ = $1; }
;

blankNodePropertyList:
    GT_LBRACKET {
	$<p_SubjectPredicatePair>$.subject = driver.curSubject;
	$<p_SubjectPredicatePair>$.predicate = driver.curPredicate;
	driver.curSubject = driver.createBNode();
      } predicateObjectList GT_RBRACKET	{
	  $$ = (BNode*)driver.curSubject; // could store w/ type in ctx..
	  driver.curSubject = $<p_SubjectPredicatePair>2.subject;
	  driver.curPredicate = $<p_SubjectPredicatePair>2.predicate;
      }
;

collection:
    // '(' object* ')'
    GT_LPAREN {
	$<p_SubjectPredicatePair>$.subject = driver.curSubject;
	$<p_SubjectPredicatePair>$.predicate = driver.curPredicate;
      } _Qobject_E_Star GT_RPAREN	{
	  if ($3 == NULL) {
	      $$ = TTerm::RDF_nil;
	  } else {
	      $$ = $3;
	      driver.curBGP->addTriplePattern
		  (driver.atomFactory->getTriple
		   (driver.curSubject, TTerm::RDF_rest, TTerm::RDF_nil));
	  }
	  driver.curSubject = $<p_SubjectPredicatePair>2.subject;
	  driver.curPredicate = $<p_SubjectPredicatePair>2.predicate;
      }
;

_Qobject_E_Star:
    {
	$$ = NULL;
    }
  | _Qobject_E_Star {
	const TTerm* nextTail = driver.createBNode();
	if ($1 == NULL) // on first element
	    $<p_TTerm>$ = nextTail;
	else		// on later elements
	    driver.curBGP->addTriplePattern
		(driver.atomFactory->getTriple
		 (driver.curSubject, TTerm::RDF_rest, nextTail));
	driver.curSubject = nextTail;
	driver.curPredicate = TTerm::RDF_first;
      } object	{
	  $$ = $<p_TTerm>2;
      }
;

exclusions:
    _Q_O_QGT_MINUS_E_S_QiriStem_E_C_E_Star	
;

_O_QGT_MINUS_E_S_QiriStem_E_C:
    GT_MINUS iriStem	{
	$$ = $2;
    }
;

_Q_O_QGT_MINUS_E_S_QiriStem_E_C_E_Star:
    {
	$$ = NULL;
    }
  | _Q_O_QGT_MINUS_E_S_QiriStem_E_C_E_Star _O_QGT_MINUS_E_S_QiriStem_E_C	{
	if ($1 == NULL)
	    $1 = new std::vector<ShExSchema::URIstem>();
	$1->push_back($2);
	$$ = $1;
    }
;

//@@^^

repeatCount:
    GT_TIMES	{
	$$.min = 0;
	$$.max = ShExSchema::AtomicRule::Unlimited;
    }
  | GT_PLUS	{
	$$.min = 1;
	$$.max = ShExSchema::AtomicRule::Unlimited;
    }
  | GT_OPT	{
	$$.min = 0;
	$$.max = 1;
    }
  | GT_LCURLEY INTEGER _Q_O_QGT_COMMA_E_S_QINTEGER_E_Opt_C_E_Opt GT_RCURLEY	{
	$$.min = $2->getInt();
	$$.max = $3;
    }
;

_QINTEGER_E_Opt:
    {
	$$ = ShExSchema::AtomicRule::Unlimited;
    }
  | INTEGER	{
	$$ = $1->getInt();
    }
;

_O_QGT_COMMA_E_S_QINTEGER_E_Opt_C:
    GT_COMMA _QINTEGER_E_Opt	{
	$$ = $2;
    }
;

_Q_O_QGT_COMMA_E_S_QINTEGER_E_Opt_C_E_Opt:
    {
	$$ = ShExSchema::AtomicRule::Unlimited;
    }
  | _O_QGT_COMMA_E_S_QINTEGER_E_Opt_C	
;

valueSet:
    GT_LPAREN _Qvalue_E_Plus GT_RPAREN	
;

_Qvalue_E_Plus:
    valueChoice	{
	driver.curTTerms->push_back($1);
    }
  | _Qvalue_E_Plus valueChoice	{
	driver.curTTerms->push_back($2);
    }
;

valueChoice:
    iriStem	{
	if ($1.opt)
	    w3c_sw_NEED_IMPL("ShEx parser support for URIstems in value sets");
	$$ = $1.uri;
    }
  | BlankNode	
  | literal	
;

NumericLiteral:
    INTEGER	{
	$$ = $1;
    }
  | DECIMAL	{
	$$ = $1;
    }
  | DOUBLE	{
	$$ = $1;
    }
;

RDFLiteral:
    // String ( LANGTAG | ( '^^' iri ) )?
    String _Q_O_QLANGTAG_E_Or_QGT_DTYPE_E_S_Qiri_E_C_E_Opt	{
	$$ = driver.getRDFLiteral(*$1, $2.uri, $2.langtag);
	delete $1;
    }
;

// LANGTAG | ( '^^' iri )
_O_QLANGTAG_E_Or_QGT_DTYPE_E_S_Qiri_E_C:
    LANGTAG	{
	$$.uri = NULL;
	$$.langtag = $1;
    }
// '^^' iri
  | GT_DTYPE iri	{
	$$.uri = $2;
	$$.langtag = NULL;
    }
;

// ( LANGTAG | ( '^^' iri ) )?
_Q_O_QLANGTAG_E_Or_QGT_DTYPE_E_S_Qiri_E_C_E_Opt:
    {
	$$.uri = NULL;
	$$.langtag = NULL;
    }
  | _O_QLANGTAG_E_Or_QGT_DTYPE_E_S_Qiri_E_C	
;

BooleanLiteral:
    IT_true	
  | IT_false	
;

String:
    STRING_LITERAL1	
  | STRING_LITERAL2	
  | STRING_LITERAL_LONG1	
  | STRING_LITERAL_LONG2	
;

iri:
    IRIREF	
  | PrefixedName	
;

PrefixedName:
    PNAME_LN	
  | PNAME_NS	
;

BlankNode:
    BLANK_NODE_LABEL	
  | ANON	
;

 /*** END ShEx language structure. ***/

%% /*** Additional Code ***/

void w3c_sw::ShExParser::error(const ShExParser::location_type& l,
			    const std::string& m)
{
    std::string s = m;
    _SUBS(s, "GT_DOT", "'.'");
    _SUBS(s, "GT_SEMI", "';'");
    _SUBS(s, "GT_LBRACKET", "'['");
    _SUBS(s, "GT_RBRACKET", "']'");
    _SUBS(s, "GT_LPAREN", "'('");
    _SUBS(s, "GT_RPAREN", "')'");
    _SUBS(s, "GT_DTYPE", "'^^'");
    _SUBS(s, "IT_true", "boolean true");
    _SUBS(s, "IT_false", "boolean false");
    _SUBS(s, "IT_PREFIX", "PREFIX");
    _SUBS(s, "IT_BASE", "BASE");
    _SUBS(s, "BASE", "@base");
    _SUBS(s, "PREFIX", "@prefix");
    _SUBS(s, "RDF_TYPE", "keyword 'a'");
    _SUBS(s, "IRIREF", "IRI");
    _SUBS(s, "PNAME_NS", "namespace prefix");
    _SUBS(s, "PNAME_LN", "local name");
    _SUBS(s, "BLANK_NODE_LABEL", "blank node label");
    _SUBS(s, "LANGTAG", "language tag");
    _SUBS(s, "INTEGER", "integer");
    _SUBS(s, "DECIMAL", "decimal");
    _SUBS(s, "DOUBLE", "double");
    _SUBS(s, "STRING_LITERAL1", "'string'");
    _SUBS(s, "STRING_LITERAL2", "\"string\"");
    _SUBS(s, "STRING_LITERAL_LONG1", "'''string'''");
    _SUBS(s, "STRING_LITERAL_LONG2", "\"\"\"string\"\"\"");
    _SUBS(s, "ANON", "anonymous blank node");
    driver.error(l, s);
}

/* START Driver (@@ stand-alone would allow it to be shared with other parsers */

namespace w3c_sw {

size_t ShExDriver::DefaultAbortErrorCount = 100;

ShExSchema* ShExDriver::parse (IStreamContext& in) {
    reset();
    streamname = in.nameStr;
    // clear prefixes for nth run but keep them around for e.g. serialization.
    clearPrefixes();

    ShExScanner scanner(this, in.p);
    scanner.set_debug(trace_scanning);
    this->lexer = &scanner;

    ShExParser parser(*this);
    parser.set_debug_level(trace_parsing);
    parser.parse();
    checkErrors();
    return curSchema;
}

ShExSchema* ShExDriver::parse (IStreamContext& in, ShExSchema* schema) {
    setSchema(schema);
    return parse(in);
}

ShExSchema* ShExDriver::parse (std::string swsexStr, ShExSchema* schema) {
    IStreamContext in(swsexStr.c_str(), IStreamContext::STRING);
    setSchema(schema);
    return parse(in);
}

} // namespace w3c_sw

/* END Driver */


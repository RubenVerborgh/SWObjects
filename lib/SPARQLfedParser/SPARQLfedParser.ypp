/* $Id: SPARQLfedParser.ypp,v 1.3 2008-11-13 19:18:01 eric Exp $ -*- mode: c++ -*- */

/* Bison parser for SPARQL + SPARUL
 * conventions:
 *   generated production names are composed of their constituents.
 *   { $$ = $1; } elided.
 *   driver has extra state for hard-to-generate tries like  Foo Bar* => ((Foo Bar) Bar)
 */

/*** yacc/bison Declarations ***/

/* Require bison 2.3 or later */
%require "2.3"

/* add debug output code to generated parser. disable this for release
 * versions. */
%debug

/* start symbol is named "start" */
%start Query

/* write out a header file containing the token defines */
%defines

/* use newer C++ skeleton file */
%skeleton "lalr1.cc"

/* namespace to enclose parser in */
%name-prefix="w3c_sw"

/* set the parser's class identifier */
%define "parser_class_name" "SPARQLfedParser"

/* keep track of the current position within the input */
%locations
%initial-action
{
    // initialize the initial location object
    @$.begin.filename = @$.end.filename = &driver.streamname;
};

/* The driver is passed by reference to the parser and to the scanner. This
 * provides a simple but effective pure interface, not relying on global
 * variables. */
%parse-param { class SPARQLfedDriver& driver }

/* verbose error messages */
%error-verbose

%{ /*** C/C++ Declarations ***/
/* Bison seems to test inclusion with PARSER_HEADER_H, rather than something
 * which varies by parser_class_name . Overriding with define specific to
 * this parser.
 */
#endif /* !PARSER_HEADER_H */
#ifndef SPARQLFED_PARSER_HPP
#define SPARQLFED_PARSER_HPP

#include "SWObjects.hpp"
#include "ParserCommon.hpp"

namespace w3c_sw {

class SPARQLfedScanner;

/** The Driver class brings together all components. It creates an instance of
 * the SPARQLfedParser and SPARQLfedScanner classes and connects them. Then the input stream is
 * fed into the scanner object and the parser gets it's token
 * sequence. Furthermore the driver object is available in the grammar rules as
 * a parameter. Therefore the driver class contains a reference to the
 * structure into which the parsed data is saved. */

class SPARQLfedDriver : public Driver {
    friend class SPARQLfedParser;
protected:
    const POS* curSubject;
    const POS* curPredicate;
    BasicGraphPattern* curBGP;
    BasicGraphPattern* neededBGP; // set to NULL as soon as it's returned as $$ anywhere
    TableOperation* triplesBlockOpt; // needed to make right-descending tree for TriplesBlock? ( ( GraphPatternNotTriples | Filter ) '.'? TriplesBlock? )*
    BindingClause* curBindingClause;
    Binding* curBinding;
    ProductionVector<const Expression*>* curExprList;
    const POS* curGraphName;

    BasicGraphPattern* makeBasicGraphPattern ( ) {
	if (curBGP == NULL) {
	    curBGP = neededBGP = curGraphName == NULL ? 
		static_cast<BasicGraphPattern*>(new DefaultGraphPattern()) : 
		static_cast<BasicGraphPattern*>(new NamedGraphPattern(curGraphName));
	}
	return curBGP;
    }

    TableOperation* makeConjunction (TableOperation* l, TableOperation* r, bool nullable) {
	if (l != NULL) {
	    if (r != NULL) {
		TableConjunction* ret = new TableConjunction();
		ret->addTableOperation(l, unnestTree);
		ret->addTableOperation(r, unnestTree);
		return ret;
	    } else
		return l;
	}
	if (r != NULL)
	    return r;
	if (!nullable)
	    return makeBasicGraphPattern();
	return NULL;
    }

    TableOperation* makeDisjunction (TableOperation* l, TableOperation* r) {
	TableDisjunction* ret = new TableDisjunction();
	ret->addTableOperation(l, unnestTree);
	ret->addTableOperation(r, unnestTree);
	return ret;
    }

public:
    SPARQLfedDriver(std::string baseURI, POSFactory* posFactory);
    ~SPARQLfedDriver();

    virtual bool parse_stream(std::istream& in,
		      const std::string& sname = "stream input");

    /** Pointer to the current lexer instance, this is used to connect the
     * parser to the scanner. It is used in the yylex macro. */
    class SPARQLfedScanner* lexer;

    /** Reference to the object created during parsing of the SPARQL stream. */
    Operation* root;

    /** Control attributes. */
    bool unnestTree;	/* conj(conj(A, B), C) => conj(A, B, C) */
};

    class FilterHolder : public BasicGraphPattern {
	ProductionVector<const Filter*> m_Filters;
    public:
	FilterHolder () : BasicGraphPattern(false) {  }
	~FilterHolder () { m_Filters.clear(); }
	//size_t filters () { return m_Filters.size(); }
	void addFilter (const Filter* filter) {
	    m_Filters.push_back(filter);
	}
	void copyFiltersTo (TableOperation* target) {
	    for (std::vector<const Filter*>::const_iterator it = m_Filters.begin();
		 it != m_Filters.end(); ++it)
		; //target->addFilter(*it);
	}
	virtual void bindVariables(RdfDB*, ResultSet*) const {  }
	virtual void express(Expressor* /* p_expressor */) const {  }
	virtual bool operator==(const TableOperation& /* ref */) const { return false; }
	virtual TableOperation* getDNF() const { return NULL; }
    };

} // namespace w3c_sw

%}

 /*** BEGIN SPARQLfed - Change the grammar's tokens below ***/

%union {
    struct {const POS* subject; const POS* predicate;} p_SubjectPredicatePair;
    struct {int limit; int offset;} p_LimitOffsetPair;
    struct {const URI* uri; LANGTAG* langtag;} p_uri_or_langtag;
    struct {Operation* operation; WhereClause* whereClause;} p_OperationWhereClausePair;
    s_OrderConditionPair p_OrderConditionPair;
    std::vector<s_OrderConditionPair>* p_OrderConditions;
    TableConjunction* p_TableConjunction;
    TableDisjunction* p_TableDisjunction;

    /* Terminals */
    LANGTAG* p_LANGTAG;

    std::string* p_string;

    const NumericRDFLiteral* p_NumericRDFLiteral;
    const BooleanRDFLiteral* p_BooleanRDFLiteral;

    /* Productions */
    Operation* p_Operation;
    const Variable* p_Variable;

    Select* p_Select;
    e_distinctness p_distinctness;
    e_listModifier p_listModifier;
    e_Silence p_Silence;
    POSList* p_POSList;
    VarSet* p_VarSet;
    ProductionVector<const DatasetClause*>* p_DatasetClauses;
    Construct* p_Construct;
    Describe* p_Describe;
    Ask* p_Ask;
    DatasetClause* p_DatasetClause;
    DefaultGraphClause* p_DefaultGraphClause;
    NamedGraphClause* p_NamedGraphClause;
    WhereClause* p_WhereClause;
    SolutionModifier* p_SolutionModifier;
    e_ASCorDESC p_e_ASCorDESC;
    BindingClause* p_BindingClause;
    ProductionVector<const Binding*>* p_Bindings;
    Binding* p_Binding;
    TableOperation* p_TableOperation;
    ProductionVector<const TableOperation*>* p_TableOperations;
    OptionalGraphPattern* p_OptionalGraphPattern;
    BasicGraphPattern* p_BasicGraphPattern;
    FilterHolder* p_FilterHolder;
    FunctionCall* p_FunctionCall;
    ArgList* p_ArgList;
    const POS* p_POS;
    ProductionVector<const POS*>* p_POSs;
    Expression* p_Expression;
    ProductionVector<const Expression*>* p_Expressions;
    BooleanComparator* p_BooleanComparator;
    const URI* p_URI;
    ProductionVector<const URI*>* p_URIs;
    const RDFLiteral* p_RDFLiteral;
    const BNode* p_BNode;

}

%{
#include "../SPARQLfedScanner.hpp"
%}
%token			__EOF__	     0	"end of file"

/* Language tokens */
%token IT_REPLACE IT_WITH IT_DELETE IT_INSERT IT_GRAPH IT_LOAD IT_INTO IT_CLEAR
       IT_CREATE IT_SILENT IT_DROP IT_BASE IT_PREFIX IT_SELECT IT_DISTINCT
       IT_REDUCED GT_TIMES IT_CONSTRUCT IT_DESCRIBE IT_FROM IT_NAMED IT_ORDER
       IT_BY IT_ASC IT_DESC IT_LIMIT IT_OFFSET IT_BINDINGS IT_MEMBERS IT_STARTS
       IT_ENDS IT_ANY IT_UNORDERED GT_LCURLEY GT_RCURLEY IT_ASK IT_WHERE
       GT_LPAREN GT_RPAREN IT_NULL GT_DOT IT_OPTIONAL IT_UNION IT_FILTER
       GT_COMMA GT_SEMI IT_a GT_LBRACKET GT_RBRACKET GT_OR GT_AND GT_EQUAL
       GT_NEQUAL GT_LT GT_GT GT_LE GT_GE GT_PLUS GT_MINUS GT_DIVIDE GT_NOT
       IT_STR IT_LANG IT_LANGMATCHES IT_DATATYPE IT_BOUND IT_sameTerm IT_isIRI
       IT_isURI IT_isBLANK IT_isLITERAL IT_REGEX GT_DTYPE

%token <p_BooleanRDFLiteral> IT_true IT_false
%token <p_NumericRDFLiteral> INTEGER DECIMAL DOUBLE INTEGER_POSITIVE
			     DECIMAL_POSITIVE DOUBLE_POSITIVE INTEGER_NEGATIVE
			     DECIMAL_NEGATIVE DOUBLE_NEGATIVE
%token <p_string> STRING_LITERAL1 STRING_LITERAL_LONG1
		  STRING_LITERAL2 STRING_LITERAL_LONG2
%token <p_URI> IRI_REF PNAME_NS PNAME_LN
%token <p_POS> BLANK_NODE_LABEL ANON
%token <p_Variable> VAR1 VAR2
%token <p_LANGTAG> LANGTAG
%token <p_NIL> NIL

/* Productions */
%type <p_Operation> _O_QSelect_E_Or_QConstruct_E_Or_QDescribe_E_Or_QAsk_E_Or_QReplace_E_Or_QUpdate_E_Or_QManage_E_C
		    Query Select Construct Describe Ask  // SPARQL 1
		    Update Manage Replace Delete Insert Load Clear Create Drop // SPARUL

%type <p_distinctness> _O_QIT_DISTINCT_E_Or_QIT_REDUCED_E_C
		       _Q_O_QIT_DISTINCT_E_Or_QIT_REDUCED_E_C_E_Opt
%type <p_listModifier> _O_QIT_MEMBERS_E_Or_QIT_STARTS_E_Or_QIT_ENDS_E_Or_QIT_ANY_E_Or_QIT_UNORDERED_E_C
		       _Q_O_QIT_MEMBERS_E_Or_QIT_STARTS_E_Or_QIT_ENDS_E_Or_QIT_ANY_E_Or_QIT_UNORDERED_E_C_E_Opt
%type <p_Silence> _QIT_SILENT_E_Opt

%type <p_URIs> _QIRIref_E_Plus
%type <p_POSList> _QVar_E_Plus _QVarOrIRIref_E_Plus
%type <p_VarSet> _O_QVar_E_Plus_Or_QGT_TIMES_E_C _O_QVarOrIRIref_E_Plus_Or_QGT_TIMES_E_C
%type <p_DatasetClauses> _QDatasetClause_E_Star
%type <p_DatasetClause> DatasetClause
			_O_QDefaultGraphClause_E_Or_QNamedGraphClause_E_C
			DefaultGraphClause NamedGraphClause
%type <p_WhereClause> _QWhereClause_E_Opt WhereClause
%type <p_SolutionModifier> SolutionModifier
%type <p_LimitOffsetPair> LimitOffsetClauses _QLimitOffsetClauses_E_Opt
			  _QLimitClause_E_S_QOffsetClause_E_Opt_Or_QOffsetClause_E_S_QLimitClause_E_Opt
			  LimitClause _QLimitClause_E_Opt
			  OffsetClause _QOffsetClause_E_Opt
%type <p_OrderConditions> _QOrderClause_E_Opt OrderClause
			  _QOrderCondition_E_Plus
%type <p_OrderConditionPair> OrderCondition
			     _O_QIT_ASC_E_Or_QIT_DESC_E_S_QBrackettedExpression_E_C
%type <p_e_ASCorDESC> _O_QIT_ASC_E_Or_QIT_DESC_E_C
%type <p_BindingClause> BindingClause _QBindingClause_E_Opt
%type <p_Bindings> _QBinding_E_Star
%type <p_Binding> Binding

%type <p_TableOperation> Filter GroupGraphPattern
			 _O_QIT_UNION_E_S_QGroupGraphPattern_E_C
			 _O_QGraphPatternNotTriples_E_Or_QFilter_E_S_QGT_DOT_E_Opt_S_QTriplesBlock_E_Opt_C
			 _O_QGraphPatternNotTriples_E_Or_QFilter_E_C
			 GraphPatternNotTriples GroupOrUnionGraphPattern
			 OptionalGraphPattern GraphGraphPattern
			 _QTriplesBlock_E_Opt TriplesBlock
			 _O_QGT_DOT_E_S_QTriplesBlock_E_Opt_C
			 _Q_O_QGT_DOT_E_S_QTriplesBlock_E_Opt_C_E_Opt
			 GraphTemplate
			 _O_QIT_GRAPH_E_S_QVarOrIRIref_E_S_QGraphTemplate_E_C
			 _O_QIT_GRAPH_E_S_QVarOrIRIref_E_S_QGraphTemplate_E_S_QGT_DOT_E_Opt_S_QTriplesBlock_E_Opt_C
			 _Q_O_QGraphPatternNotTriples_E_Or_QFilter_E_S_QGT_DOT_E_Opt_S_QTriplesBlock_E_Opt_C_E_Star
			 _Q_O_QIT_UNION_E_S_QGroupGraphPattern_E_C_E_Star
			 _Q_O_QIT_GRAPH_E_S_QVarOrIRIref_E_S_QGraphTemplate_E_S_QGT_DOT_E_Opt_S_QTriplesBlock_E_Opt_C_E_Star

%type <p_BasicGraphPattern> ConstructTemplate

%type <p_ArgList> ArgList _QArgList_E_Opt
%type <p_Expressions> _O_QNIL_E_Or_QGT_LPAREN_E_S_QExpression_E_S_QGT_COMMA_E_S_QExpression_E_Star_S_QGT_RPAREN_E_C
		      _Q_O_QGT_OR_E_S_QConditionalAndExpression_E_C_E_Star
		      _Q_O_QGT_AND_E_S_QValueLogical_E_C_E_Star
		      _Q_O_QGT_PLUS_E_S_QMultiplicativeExpression_E_Or_QGT_MINUS_E_S_QMultiplicativeExpression_E_Or_QNumericLiteralPositive_E_Or_QNumericLiteralNegative_E_C_E_Star
		      _Q_O_QGT_TIMES_E_S_QUnaryExpression_E_Or_QGT_DIVIDE_E_S_QUnaryExpression_E_C_E_Star

 /* Parts Of Speach */
%type <p_POS> SourceSelector
	      _O_QVarOrTerm_E_Or_QIT_NULL_E_C
	      BlankNodePropertyList
	      Verb VarOrIRIref VarOrTerm GraphNode Object
	      GraphTerm BlankNode TriplesNode Generator
%type <p_POSs> _QGraphNode_E_Star _O_QGraphNode_E_S_QGraphNode_E_Star_C
	       _Q_O_QGraphNode_E_S_QGraphNode_E_Star_C_E_Opt
	       _Q_O_QVarOrTerm_E_Or_QIT_NULL_E_C_E_Plus
%type <p_RDFLiteral> RDFLiteral
%type <p_NumericRDFLiteral> NumericLiteralUnsigned NumericLiteralPositive
			    NumericLiteralNegative NumericLiteral
%type <p_BooleanRDFLiteral> BooleanLiteral
%type <p_string> String
%type <p_Variable> Var

/* Expressions */
%type <p_Expression> Expression Constraint _O_QConstraint_E_Or_QVar_E_C
		     BrackettedExpression BuiltInCall FunctionCall
		     ConditionalOrExpression
		     _O_QGT_OR_E_S_QConditionalAndExpression_E_C
		     ConditionalAndExpression _O_QGT_AND_E_S_QValueLogical_E_C
		     ValueLogical RelationalExpression NumericExpression
		     AdditiveExpression
		     _O_QGT_PLUS_E_S_QMultiplicativeExpression_E_Or_QGT_MINUS_E_S_QMultiplicativeExpression_E_Or_QNumericLiteralPositive_E_Or_QNumericLiteralNegative_E_C
		     MultiplicativeExpression
		     _O_QGT_TIMES_E_S_QUnaryExpression_E_Or_QGT_DIVIDE_E_S_QUnaryExpression_E_C
		     UnaryExpression PrimaryExpression RegexExpression
		     _O_QGT_COMMA_E_S_QExpression_E_C
		     _Q_O_QGT_COMMA_E_S_QExpression_E_C_E_Opt IRIrefOrFunction

%type <p_BooleanComparator> _O_QGT_EQUAL_E_S_QNumericExpression_E_Or_QGT_NEQUAL_E_S_QNumericExpression_E_Or_QGT_LT_E_S_QNumericExpression_E_Or_QGT_GT_E_S_QNumericExpression_E_Or_QGT_LE_E_S_QNumericExpression_E_Or_QGT_GE_E_S_QNumericExpression_E_C
			    _Q_O_QGT_EQUAL_E_S_QNumericExpression_E_Or_QGT_NEQUAL_E_S_QNumericExpression_E_Or_QGT_LT_E_S_QNumericExpression_E_Or_QGT_GT_E_S_QNumericExpression_E_Or_QGT_LE_E_S_QNumericExpression_E_Or_QGT_GE_E_S_QNumericExpression_E_C_E_Opt

%type <p_URI> _O_QGT_DTYPE_E_S_QIRIref_E_C IRIref PrefixedName GraphIRI
	      _QGraphIRI_E_Opt _Q_O_QIT_INTO_E_S_QIRIref_E_C_E_Opt
	      _O_QIT_INTO_E_S_QIRIref_E_C
%type <p_uri_or_langtag> _O_QLANGTAG_E_Or_QGT_DTYPE_E_S_QIRIref_E_C
			 _Q_O_QLANGTAG_E_Or_QGT_DTYPE_E_S_QIRIref_E_C_E_Opt

/* END TokenBlock */

//%destructor { delete $$; } BlankNode

 /*** END SPARQLfed - Change the grammar's tokens above ***/

%{
#include "../SPARQLfedScanner.hpp"

/* this "connects" the bison parser in the driver to the flex scanner class
 * object. it defines the yylex() function call to pull the next token from the
 * current lexer object of the driver context. */
#undef yylex
#define yylex driver.lexer->lex
%}

%% /*** Grammar Rules ***/

 /*** BEGIN SPARQLfed - Change the grammar rules below ***/
Query:
    // Prologue ( Select | Construct | Describe | Ask | Replace | Update | Magage )
    Prologue _O_QSelect_E_Or_QConstruct_E_Or_QDescribe_E_Or_QAsk_E_Or_QReplace_E_Or_QUpdate_E_Or_QManage_E_C	{
	driver.root = $$ = $2;
    }
;

// Select | Construct | Describe | Ask | Replace | Update | Magage
_O_QSelect_E_Or_QConstruct_E_Or_QDescribe_E_Or_QAsk_E_Or_QReplace_E_Or_QUpdate_E_Or_QManage_E_C:
    Select	
  | Construct	
  | Describe	
  | Ask	
  | Replace	
  | Update	
  | Manage	
;

Update:
    Insert	
  | Delete	
  | Load	
  | Clear	
;

Replace:
    IT_REPLACE WhereClause IT_WITH GraphTemplate	{
	$$ = new Replace($2, $4);
    }
;

Insert:
    IT_INSERT GraphTemplate _QWhereClause_E_Opt	{
	$$ = new Insert($2, $3);
}
;

Delete:
    IT_DELETE GraphTemplate _QWhereClause_E_Opt	{
	$$ = new Delete($2, $3);
    }
;

Load:
    IT_LOAD _QIRIref_E_Plus _Q_O_QIT_INTO_E_S_QIRIref_E_C_E_Opt	{
	$$ = new Load($2, $3);
    }
;

Clear:
    IT_CLEAR _QGraphIRI_E_Opt	{
	$$ = new Clear($2);
    }
;

// IRIref+
_QIRIref_E_Plus:
    IRIref	{
	$$ = new ProductionVector<const URI*>($1);
    }
  | _QIRIref_E_Plus IRIref	{
	$1->push_back($2);
	$$ = $1;
    }
;

// 'INTO' IRIref
_O_QIT_INTO_E_S_QIRIref_E_C:
    IT_INTO IRIref	{
	$$ = $2;
    }
;

// ( 'INTO' IRIref )?
_Q_O_QIT_INTO_E_S_QIRIref_E_C_E_Opt:
    {
	$$ = NULL;
    }
  | _O_QIT_INTO_E_S_QIRIref_E_C	
;

GraphIRI:
    IT_GRAPH IRIref	{
	$$ = $2;
    }
;

_QGraphIRI_E_Opt:
    {
	$$ = NULL;
    }
  | GraphIRI	
;

Manage:
    Create	
  | Drop	
;

Create:
    IT_CREATE _QIT_SILENT_E_Opt GraphIRI	{
	$$ = new Create($2, $3);
    }
;

_QIT_SILENT_E_Opt:
    {
	$$ = SILENT_No;
    }
  | IT_SILENT	{
	$$ = SILENT_Yes;
}
;

Drop:
    IT_DROP _QIT_SILENT_E_Opt GraphIRI	{
	$$ = new Drop($2, $3);
}
;

Prologue:
    // BaseDecl? PrefixDecl*
    _QBaseDecl_E_Opt _QPrefixDecl_E_Star
;

// BaseDecl?
_QBaseDecl_E_Opt:

    | BaseDecl
;

// PrefixDecl*
_QPrefixDecl_E_Star:

    | _QPrefixDecl_E_Star PrefixDecl
;

BaseDecl:
    IT_BASE IRI_REF	{
	driver.setBase($2);
    }
;

PrefixDecl:
    IT_PREFIX {
	driver.ignorePrefix(true);
      } PNAME_NS {
	  driver.ignorePrefix(false);
      } IRI_REF	{
	  std::string prefix($3->getLexicalValue());
	  driver.addPrefix(prefix.substr(0, prefix.length()-1), $5);
      }
;

Select:
    // 'SELECT' ( 'DISTINCT' | 'REDUCED' )? ( Var+ | '*' ) DatasetClause* WhereClause SolutionModifier
    IT_SELECT _Q_O_QIT_DISTINCT_E_Or_QIT_REDUCED_E_C_E_Opt _O_QVar_E_Plus_Or_QGT_TIMES_E_C _QDatasetClause_E_Star WhereClause SolutionModifier	{
	$$ = new Select($2, $3, $4, $5, $6);
    }
;

// 'DISTINCT' | 'REDUCED'
_O_QIT_DISTINCT_E_Or_QIT_REDUCED_E_C:
    IT_DISTINCT	{
	$$ = DIST_distinct;
    }
  | IT_REDUCED	{
	$$ = DIST_reduced;
    }
;

// ( 'DISTINCT' | 'REDUCED' )?
_Q_O_QIT_DISTINCT_E_Or_QIT_REDUCED_E_C_E_Opt:
    {
	$$ = DIST_all;
    }
  | _O_QIT_DISTINCT_E_Or_QIT_REDUCED_E_C	
;

// Var+
_QVar_E_Plus:
    Var	{
	$$ = new POSList;
	$$->push_back($1);
    }
  | _QVar_E_Plus Var	{
	$1->push_back($2);
	$$ = $1;
    }
;

// Var+ | '*'
_O_QVar_E_Plus_Or_QGT_TIMES_E_C:
    _QVar_E_Plus	{
	$$ = $1;
    }
  | GT_TIMES	{
	$$ = new StarVarSet();
    }
;

// DatasetClause*
_QDatasetClause_E_Star:
    {
	$$ = new ProductionVector<const DatasetClause*>();
    }
  | _QDatasetClause_E_Star DatasetClause	{
	$1->push_back($2);
	$$ = $1;
    }
;

Construct:
    // 'CONSTRUCT' ConstructTemplate DatasetClause* WhereClause SolutionModifier
    IT_CONSTRUCT ConstructTemplate _QDatasetClause_E_Star WhereClause SolutionModifier	{
	/* $2 is known to be a DefaultGraphPattern because of grammar restrictions. */
	$$ = new Construct((DefaultGraphPattern*)$2, $3, $4, $5);
    }
;

Describe:
    // 'DESCRIBE' ( VarOrIRIref+ | '*' ) DatasetClause* WhereClause? SolutionModifier
    IT_DESCRIBE _O_QVarOrIRIref_E_Plus_Or_QGT_TIMES_E_C _QDatasetClause_E_Star _QWhereClause_E_Opt SolutionModifier	{
	$$ = new Describe($2, $3, $4, $5);
    }
;

// VarOrIRIref+
_QVarOrIRIref_E_Plus:
    VarOrIRIref	{
	$$ = new POSList;
	$$->push_back($1);
    }
  | _QVarOrIRIref_E_Plus VarOrIRIref	{
	$1->push_back($2);
	$$ = $1;
    }
;

// VarOrIRIref+ | '*'
_O_QVarOrIRIref_E_Plus_Or_QGT_TIMES_E_C:
    _QVarOrIRIref_E_Plus	{
	$$ = $1;
    }
  | GT_TIMES	{
	$$ = new StarVarSet();
    }
;

// 'WHERE'?
_QWhereClause_E_Opt:
    {
	$$ = NULL;
    }
  | WhereClause	
;

Ask:
    // 'ASK' DatasetClause* WhereClause
    IT_ASK _QDatasetClause_E_Star WhereClause	{
	$$ = new Ask($2, $3);
    }
;

DatasetClause:
    // 'FROM' ( DefaultGraphClause | NamedGraphClause )
    IT_FROM _O_QDefaultGraphClause_E_Or_QNamedGraphClause_E_C	{
	$$ = $2;
    }
;

// DefaultGraphClause | NamedGraphClause
_O_QDefaultGraphClause_E_Or_QNamedGraphClause_E_C:
    DefaultGraphClause	
  | NamedGraphClause	
;

DefaultGraphClause:
    SourceSelector	{
	$$ = new DefaultGraphClause($1, driver.posFactory);
    }
;

NamedGraphClause:
    IT_NAMED SourceSelector	{
	$$ = new NamedGraphClause($2, driver.posFactory);
    }
;

SourceSelector:
    IRIref	{
	$$ = $1;
    }
;

WhereClause:
    // 'WHERE'? GroupGraphPattern BindingClause?
    _QIT_WHERE_E_Opt GroupGraphPattern _QBindingClause_E_Opt	{
	$$ = new WhereClause($2, $3);
    }
;

_QIT_WHERE_E_Opt:

    | IT_WHERE	;

// BindingClause?
_QBindingClause_E_Opt:
    {
	$$ = NULL;
    }
  | BindingClause	
;

SolutionModifier:
    // OrderClause? LimitOffsetClauses?
    _QOrderClause_E_Opt _QLimitOffsetClauses_E_Opt	{
	$$ = new SolutionModifier($1, $2.limit, $2.offset);
    }
;

// OrderClause?
_QOrderClause_E_Opt:
    {
	$$ = NULL;
    }
  | OrderClause	
;

// LimitOffsetClauses?
_QLimitOffsetClauses_E_Opt:
    {
	$$.limit = LIMIT_None;
	$$.offset = OFFSET_None;
    }
  | LimitOffsetClauses
;

LimitOffsetClauses:
    // LimitClause OffsetClause? | OffsetClause LimitClause?
    _QLimitClause_E_S_QOffsetClause_E_Opt_Or_QOffsetClause_E_S_QLimitClause_E_Opt
;

// OffsetClause?
_QOffsetClause_E_Opt:
    {
	$$.offset = OFFSET_None;
    }
  | OffsetClause	
;

// LimitClause?
_QLimitClause_E_Opt:
    {
	$$.limit = LIMIT_None;
    }
  | LimitClause	
;

// LimitClause OffsetClause? | OffsetClause LimitClause?
_QLimitClause_E_S_QOffsetClause_E_Opt_Or_QOffsetClause_E_S_QLimitClause_E_Opt:
    LimitClause _QOffsetClause_E_Opt	{
	$$.limit = $1.limit;
	$$.offset = $2.offset;
    }
  | OffsetClause _QLimitClause_E_Opt	{
	$$.limit = $2.limit;
	$$.offset = $1.offset;
    }
;

OrderClause:
    // 'ORDER' 'BY' OrderCondition+
    IT_ORDER IT_BY _QOrderCondition_E_Plus	{
	$$ = $3;
    }
;

// OrderCondition+
_QOrderCondition_E_Plus:
    OrderCondition	{
	$$ = new std::vector<s_OrderConditionPair>();
	$$->push_back($1);
    }
  | _QOrderCondition_E_Plus OrderCondition	{
	$1->push_back($2);
	$$ = $1;
    }
;

OrderCondition:
    // ( 'ASC' | 'DESC' ) BrackettedExpression
    _O_QIT_ASC_E_Or_QIT_DESC_E_S_QBrackettedExpression_E_C
  | // Constraint | Var
    _O_QConstraint_E_Or_QVar_E_C	{
	$$.ascOrDesc = ORDER_Asc;
	$$.expression = $1;
    }
;

// 'ASC' | 'DESC'
_O_QIT_ASC_E_Or_QIT_DESC_E_C:
    IT_ASC	{
	$$ = ORDER_Asc;
    }
  | IT_DESC	{
	$$ = ORDER_Desc;
    }
;

// ( 'ASC' | 'DESC' ) BrackettedExpression
_O_QIT_ASC_E_Or_QIT_DESC_E_S_QBrackettedExpression_E_C:
    // 'ASC' | 'DESC'
    _O_QIT_ASC_E_Or_QIT_DESC_E_C BrackettedExpression	{
	$$.ascOrDesc = $1;
	$$.expression = $2;
    }
;

// Constraint | Var
_O_QConstraint_E_Or_QVar_E_C:
    Constraint	
  | Var	{
	$$ = new VarExpression($1);
    }
;

LimitClause:
    IT_LIMIT INTEGER	{
	$$.limit = ((IntegerRDFLiteral*)$2)->getValue();
    }
;

OffsetClause:
    IT_OFFSET INTEGER	{
	$$.offset = ((IntegerRDFLiteral*)$2)->getValue();
    }
;

BindingClause:
    // 'BINDINGS' Var+ '{' Binding* '}'
    IT_BINDINGS _QVar_E_Plus GT_LCURLEY {
	driver.curBindingClause = new BindingClause($2);
      } _QBinding_E_Star GT_RCURLEY	{
	  $$ = driver.curBindingClause;
	  driver.curBindingClause = NULL;
      }
;

// Binding*
_QBinding_E_Star:
    {
	$$ = NULL;
    }
  | _QBinding_E_Star Binding	{
	driver.curBindingClause->push_back($2);
	$$ = NULL;
    }
;

Binding:
    // '(' ( VarOrTerm | 'NULL' )+ ')"/
    GT_LPAREN {
	driver.curBinding = new Binding();
      } _Q_O_QVarOrTerm_E_Or_QIT_NULL_E_C_E_Plus GT_RPAREN	{
	  $$ = driver.curBinding;
	  driver.curBinding = NULL;
      }
;

// VarOrTerm | 'NULL'
_O_QVarOrTerm_E_Or_QIT_NULL_E_C:
    VarOrTerm	
  | IT_NULL	{
	$$ = driver.getNULL();
    }
;

// ( VarOrTerm | 'NULL' )+
_Q_O_QVarOrTerm_E_Or_QIT_NULL_E_C_E_Plus:
    _O_QVarOrTerm_E_Or_QIT_NULL_E_C	{
	driver.curBinding->push_back($1);
	$$ = NULL;
    }
  | _Q_O_QVarOrTerm_E_Or_QIT_NULL_E_C_E_Plus _O_QVarOrTerm_E_Or_QIT_NULL_E_C	{
	driver.curBinding->push_back($2);
	$$ = NULL;
    }
;

GroupGraphPattern:
    // '{' TriplesBlock? ( ( GraphPatternNotTriples | Filter ) '.'? TriplesBlock? )* '}'
    GT_LCURLEY {
	driver.curBGP = driver.neededBGP = NULL;
	$<p_TableOperation>$ = driver.triplesBlockOpt;
	driver.triplesBlockOpt = NULL;
      } _QTriplesBlock_E_Opt {
	  driver.triplesBlockOpt = $3;
      } _Q_O_QGraphPatternNotTriples_E_Or_QFilter_E_S_QGT_DOT_E_Opt_S_QTriplesBlock_E_Opt_C_E_Star GT_RCURLEY	{
	  // if ($3) LINE << $3 << ": " << *$3 << "--\n";
	  // if ($5) LINE << $5 << ": " << *$5 << "--\n";
	  $$ = 
	      $5 ? $5 : 
	      $3 ? $3 :
	      driver.makeBasicGraphPattern();
	  driver.curBGP = driver.neededBGP = NULL;
	  /* Prevents "optimization" in e.g. <data-r2/algebra/join-combo-1.rq>
	   *  of ?a :p ?c . OPTIONAL { ?a :r ?d } ?a ?p 1 .
	   *  to { ?a :p ?c . ?a ?p 1 . } OPTIONAL { ?a :r ?d }
	   * now { ?a :p ?c . } OPTIONAL { ?a :r ?d } { ?a ?p 1 . }
	   */
	  driver.triplesBlockOpt = $<p_TableOperation>2;
      }
;

// TriplesBlock?
_QTriplesBlock_E_Opt:
    { $$ = NULL; }
  | TriplesBlock	
;

// GraphPatternNotTriples | Filter
_O_QGraphPatternNotTriples_E_Or_QFilter_E_C:
    GraphPatternNotTriples
  | Filter	
;

// '.'?
_QGT_DOT_E_Opt:

    | GT_DOT
;

// ( GraphPatternNotTriples | Filter ) '.'? TriplesBlock?
_O_QGraphPatternNotTriples_E_Or_QFilter_E_S_QGT_DOT_E_Opt_S_QTriplesBlock_E_Opt_C:
    _O_QGraphPatternNotTriples_E_Or_QFilter_E_C {
	$<p_TableOperation>$ = driver.triplesBlockOpt ? driver.makeConjunction(driver.triplesBlockOpt, $1, true) : $1;;
	driver.triplesBlockOpt = NULL;
      } _QGT_DOT_E_Opt _QTriplesBlock_E_Opt	{
	  $$ = driver.makeConjunction($<p_TableOperation>2, $4, true);
      }
;

// ( ( GraphPatternNotTriples | Filter ) '.'? TriplesBlock? )*
_Q_O_QGraphPatternNotTriples_E_Or_QFilter_E_S_QGT_DOT_E_Opt_S_QTriplesBlock_E_Opt_C_E_Star:
    {
	$$ = NULL;
    }
  | // ( GraphPatternNotTriples | Filter ) '.'? TriplesBlock?
    _Q_O_QGraphPatternNotTriples_E_Or_QFilter_E_S_QGT_DOT_E_Opt_S_QTriplesBlock_E_Opt_C_E_Star _O_QGraphPatternNotTriples_E_Or_QFilter_E_S_QGT_DOT_E_Opt_S_QTriplesBlock_E_Opt_C	{
	if (driver.triplesBlockOpt) {
	    $1 = driver.makeConjunction(driver.triplesBlockOpt, $1, true);
	    driver.triplesBlockOpt = NULL;
	}
	$$ = driver.makeConjunction($1, $2, false);
    }
;

GraphTemplate:
    // '{' TriplesBlock? ( ( 'GRAPH' VarOrIRIref GraphTemplate | Filter ) '.'? TriplesBlock? )* '}'
    GT_LCURLEY {
	driver.curBGP = driver.neededBGP = NULL;
      } _QTriplesBlock_E_Opt _Q_O_QIT_GRAPH_E_S_QVarOrIRIref_E_S_QGraphTemplate_E_S_QGT_DOT_E_Opt_S_QTriplesBlock_E_Opt_C_E_Star GT_RCURLEY	{
	  $$ = driver.makeConjunction($3, $4, true);
	  driver.curBGP = driver.neededBGP = NULL;
	  /* Prevents "optimization" in e.g. <data-r2/algebra/join-combo-1.rq>
	   *  of ?a :p ?c . OPTIONAL { ?a :r ?d } ?a ?p 1 .
	   *  to { ?a :p ?c . ?a ?p 1 . } OPTIONAL { ?a :r ?d }
	   * now { ?a :p ?c . } OPTIONAL { ?a :r ?d } { ?a ?p 1 . }
	   */
      }
;

// 'GRAPH' VarOrIRIref GraphTemplate
_O_QIT_GRAPH_E_S_QVarOrIRIref_E_S_QGraphTemplate_E_C:
    IT_GRAPH VarOrIRIref {
	$<p_POS>$ = driver.curGraphName;
	driver.curGraphName = $2;
    } GraphTemplate	{
	$$ = new GraphGraphPattern($2, $4);
	driver.curGraphName = $<p_POS>3;
    }
;

// ( 'GRAPH' VarOrIRIref GraphTemplate ) '.'? TriplesBlock?
_O_QIT_GRAPH_E_S_QVarOrIRIref_E_S_QGraphTemplate_E_S_QGT_DOT_E_Opt_S_QTriplesBlock_E_Opt_C:
    _O_QIT_GRAPH_E_S_QVarOrIRIref_E_S_QGraphTemplate_E_C _QGT_DOT_E_Opt _QTriplesBlock_E_Opt	{
	$$ = driver.makeConjunction($1, $3, true);
    }
;

// ( ( 'GRAPH' VarOrIRIref GraphTemplate ) '.'? TriplesBlock? )*
_Q_O_QIT_GRAPH_E_S_QVarOrIRIref_E_S_QGraphTemplate_E_S_QGT_DOT_E_Opt_S_QTriplesBlock_E_Opt_C_E_Star:
    {
	$$ = NULL;
    }
  | _Q_O_QIT_GRAPH_E_S_QVarOrIRIref_E_S_QGraphTemplate_E_S_QGT_DOT_E_Opt_S_QTriplesBlock_E_Opt_C_E_Star _O_QIT_GRAPH_E_S_QVarOrIRIref_E_S_QGraphTemplate_E_S_QGT_DOT_E_Opt_S_QTriplesBlock_E_Opt_C	{
	$$ = driver.makeConjunction($1, $2, true);
    }
;

TriplesBlock:
    // TriplesSameSubject ( '.' TriplesBlock? )?
    TriplesSameSubject _Q_O_QGT_DOT_E_S_QTriplesBlock_E_Opt_C_E_Opt	{
	$$ = driver.neededBGP;
	//	driver.neededBGP = NULL;
    }
;

// '.' TriplesBlock?
_O_QGT_DOT_E_S_QTriplesBlock_E_Opt_C:
    GT_DOT _QTriplesBlock_E_Opt	{
	$$ = $2;
    }
;

// ( '.' TriplesBlock? )?
_Q_O_QGT_DOT_E_S_QTriplesBlock_E_Opt_C_E_Opt:
    { $$ = NULL; }
  | // '.' TriplesBlock?
    _O_QGT_DOT_E_S_QTriplesBlock_E_Opt_C
;

GraphPatternNotTriples:
    OptionalGraphPattern
    | GroupOrUnionGraphPattern
    | GraphGraphPattern
;

OptionalGraphPattern:
    IT_OPTIONAL	{
	$<p_FilterHolder>$ = new FilterHolder();
	driver.curBGP = driver.neededBGP = NULL;
      } GroupGraphPattern	{
	  OptionalGraphPattern* ret = new OptionalGraphPattern($3);
	  $<p_FilterHolder>2->copyFiltersTo(ret);
	  delete $<p_FilterHolder>2;
	  driver.curBGP = driver.neededBGP = NULL;
	  $$ = ret;
      }
;

GraphGraphPattern:
    IT_GRAPH VarOrIRIref {
	$<p_POS>$ = driver.curGraphName;
	driver.curGraphName = $2;
      } GroupGraphPattern	{
	  $$ = new GraphGraphPattern($2, $4);
	  driver.curGraphName = $<p_POS>3;
	  driver.curBGP = driver.neededBGP = NULL;
      }
;

GroupOrUnionGraphPattern:
    // GroupGraphPattern ( 'UNION' GroupGraphPattern )*
    GroupGraphPattern _Q_O_QIT_UNION_E_S_QGroupGraphPattern_E_C_E_Star	{
	$$ = $2 ? driver.makeDisjunction($1, $2) : $1;
    }
;

// 'UNION' GroupGraphPattern
_O_QIT_UNION_E_S_QGroupGraphPattern_E_C:
    IT_UNION GroupGraphPattern	{
	$$ = $2;
    }
;

// ( 'UNION' GroupGraphPattern )*
_Q_O_QIT_UNION_E_S_QGroupGraphPattern_E_C_E_Star:
    {
	$$ = NULL;
    }
  | _O_QIT_UNION_E_S_QGroupGraphPattern_E_C _Q_O_QIT_UNION_E_S_QGroupGraphPattern_E_C_E_Star	{
	$$ = $2 ? driver.makeDisjunction($1, $2) : $1;
    }
;

Filter:
    IT_FILTER Constraint	{
	$$ = new Filter($2);
	FilterHolder* h = dynamic_cast<FilterHolder*>(driver.curBGP);
#if 0
	if (h == NULL) {
	    driver.makeBasicGraphPattern();
	    driver.curBGP->addFilter($$);
	} else {
	    h->addFilter($$);
	}
#endif
    }
;

Constraint:
    BrackettedExpression	
  | BuiltInCall	
  | FunctionCall	
;

FunctionCall:
    IRIref ArgList	{
	$$ = new FunctionCallExpression(new FunctionCall($1, $2));
    }
;

ArgList:
    // ( NIL | '(' Expression ( ',' Expression )* ')' )
    _O_QNIL_E_Or_QGT_LPAREN_E_S_QExpression_E_S_QGT_COMMA_E_S_QExpression_E_Star_S_QGT_RPAREN_E_C	{
	$$ = new ArgList($1);
    }
;

// ',' Expression
_O_QGT_COMMA_E_S_QExpression_E_C:
    GT_COMMA Expression	{
	$$ = $2;
    }
;

// ( ',' Expression )*
_Q_O_QGT_COMMA_E_S_QExpression_E_C_E_Star:

  | _Q_O_QGT_COMMA_E_S_QExpression_E_C_E_Star _O_QGT_COMMA_E_S_QExpression_E_C	{
	driver.curExprList->push_back($2);
    }
;

// ( NIL | '(' Expression ( ',' Expression )* ')' )
_O_QNIL_E_Or_QGT_LPAREN_E_S_QExpression_E_S_QGT_COMMA_E_S_QExpression_E_Star_S_QGT_RPAREN_E_C:
    NIL	{
	$$ = NULL;
    }
  | // '(' Expression ( ',' Expression )* ')'
    GT_LPAREN Expression {
	driver.curExprList = new ProductionVector<const Expression*>($2);
      } _Q_O_QGT_COMMA_E_S_QExpression_E_C_E_Star GT_RPAREN	{
	  $$ = driver.curExprList;
	  driver.curExprList = NULL;
      }
;

ConstructTemplate:
    // '{' ConstructTriples? '}'
    GT_LCURLEY {
	driver.curBGP = driver.neededBGP = NULL;
      } _QConstructTriples_E_Opt GT_RCURLEY	{
	  $$ = driver.neededBGP;
	  driver.curBGP = driver.neededBGP = NULL;
      }
;

// ConstructTriples?
_QConstructTriples_E_Opt:
    
  | ConstructTriples	
;

ConstructTriples:
    // TriplesSameSubject ( '.' ConstructTriples? )?
    TriplesSameSubject _Q_O_QGT_DOT_E_S_QConstructTriples_E_Opt_C_E_Opt	
;

// '.' ConstructTriples?
_O_QGT_DOT_E_S_QConstructTriples_E_Opt_C:
    GT_DOT _QConstructTriples_E_Opt	
;

// ( '.' ConstructTriples? )?
_Q_O_QGT_DOT_E_S_QConstructTriples_E_Opt_C_E_Opt:
    
  | _O_QGT_DOT_E_S_QConstructTriples_E_Opt_C
;

TriplesSameSubject:
    VarOrTerm {
	driver.curSubject = $1;
    } PropertyListNotEmpty
  | TriplesNode {
	driver.curSubject = $1;
    } PropertyList
;

PropertyListNotEmpty:
    // Verb ObjectList ( ';' ( Verb ObjectList )? )*
    Verb ObjectList _Q_O_QGT_SEMI_E_S_QVerb_E_S_QObjectList_E_Opt_C_E_Star
;

// Verb ObjectList
_O_QVerb_E_S_QObjectList_E_C:
    Verb ObjectList
;

// ( Verb ObjectList )?
_Q_O_QVerb_E_S_QObjectList_E_C_E_Opt:

    | _O_QVerb_E_S_QObjectList_E_C
;

// ';' ( Verb ObjectList )?
_O_QGT_SEMI_E_S_QVerb_E_S_QObjectList_E_Opt_C:
    GT_SEMI _Q_O_QVerb_E_S_QObjectList_E_C_E_Opt
;

// ( ';' ( Verb ObjectList )? )*
_Q_O_QGT_SEMI_E_S_QVerb_E_S_QObjectList_E_Opt_C_E_Star:

    | _Q_O_QGT_SEMI_E_S_QVerb_E_S_QObjectList_E_Opt_C_E_Star _O_QGT_SEMI_E_S_QVerb_E_S_QObjectList_E_Opt_C
;

PropertyList:
    // PropertyListNotEmpty?
    _QPropertyListNotEmpty_E_Opt
;

// PropertyListNotEmpty?
_QPropertyListNotEmpty_E_Opt:

    | PropertyListNotEmpty
;

ObjectList:
    // Object ( ',' Object )*
    Object _Q_O_QGT_COMMA_E_S_QObject_E_C_E_Star
;

// ',' Object
_O_QGT_COMMA_E_S_QObject_E_C:
    GT_COMMA Object
;

// ( ',' Object )*
_Q_O_QGT_COMMA_E_S_QObject_E_C_E_Star:

    | _Q_O_QGT_COMMA_E_S_QObject_E_C_E_Star _O_QGT_COMMA_E_S_QObject_E_C
;

Object:
    GraphNode	{
	BasicGraphPattern* bgp = driver.makeBasicGraphPattern();
	bgp->addTriplePattern(driver.posFactory->getTriple(driver.curSubject, driver.curPredicate, $1));
	$$ = $1;
    }
;

Verb:
    VarOrIRIref	{
	$$ = $1;
	driver.curPredicate = $$;
    }
  | IT_a	{
	$$ = driver.getURI("http://www.w3.org/1999/02/22-rdf-syntax-ns#type");
	driver.curPredicate = $$;
    }
;

TriplesNode:
    Generator	
  | BlankNodePropertyList	
;

BlankNodePropertyList:
    GT_LBRACKET {
	$<p_SubjectPredicatePair>$.subject = driver.curSubject;
	$<p_SubjectPredicatePair>$.predicate = driver.curPredicate;
	driver.curSubject = driver.createBNode();
      } PropertyListNotEmpty GT_RBRACKET	{
	  $$ = driver.curSubject; // could store w/ type in ctx..
	  driver.curSubject = $<p_SubjectPredicatePair>2.subject;
	  driver.curPredicate = $<p_SubjectPredicatePair>2.predicate;
      }
;

Generator:
    // ('members' | 'starts' | 'ends' | 'any' | 'unordered')? '(' ( GraphNode GraphNode* )? ')' ;
    _Q_O_QIT_MEMBERS_E_Or_QIT_STARTS_E_Or_QIT_ENDS_E_Or_QIT_ANY_E_Or_QIT_UNORDERED_E_C_E_Opt GT_LPAREN _Q_O_QGraphNode_E_S_QGraphNode_E_Star_C_E_Opt GT_RPAREN	{
	if ($1 == LIST_exact) {
	    $$ = driver.createBNode();
	    const POS* tail = $$;
	    ProductionVector<const POS*>* members = $3;
	    const URI* first = driver.getURI("http://www.w3.org/1999/02/22-rdf-syntax-ns#first");
	    const URI* rest = driver.getURI("http://www.w3.org/1999/02/22-rdf-syntax-ns#rest");
	    const POS* nil = driver.getURI("http://www.w3.org/1999/02/22-rdf-syntax-ns#nil");
	    driver.makeBasicGraphPattern();
	    for (unsigned i = 0; i < members->size(); i++) {
		driver.curBGP->addTriplePattern(driver.posFactory->getTriple(tail, first, members->at(i)));
		const POS* nextTail = i == members->size()-1 ? nil : driver.createBNode();
		driver.curBGP->addTriplePattern(driver.posFactory->getTriple(tail, rest, nextTail));
		tail = nextTail;
	    }
	    members->clear();
	    delete members;
	} else {
	    error(*(yylocation_stack_.end()), "unsupported function");
	}
    }
;

_O_QIT_MEMBERS_E_Or_QIT_STARTS_E_Or_QIT_ENDS_E_Or_QIT_ANY_E_Or_QIT_UNORDERED_E_C:
    // 'members' | 'starts' | 'ends' | 'any' | 'unordered'
    IT_MEMBERS	{
	$$ = LIST_members;
    }
  | IT_STARTS	{
	$$ = LIST_starts;
    }
  | IT_ENDS	{
	$$ = LIST_ends;
    }
  | IT_ANY	{
	$$ = LIST_any;
    }
  | IT_UNORDERED	{
	$$ = LIST_unordered;
    }
;

_Q_O_QIT_MEMBERS_E_Or_QIT_STARTS_E_Or_QIT_ENDS_E_Or_QIT_ANY_E_Or_QIT_UNORDERED_E_C_E_Opt:
    // ('members' | 'starts' | 'ends' | 'any' | 'unordered')?
    {
	$$ = LIST_exact;
    }
  | _O_QIT_MEMBERS_E_Or_QIT_STARTS_E_Or_QIT_ENDS_E_Or_QIT_ANY_E_Or_QIT_UNORDERED_E_C	
;

// GraphNode*
_QGraphNode_E_Star:
    {
	$$ = new ProductionVector<const POS*>();
    }
  | _QGraphNode_E_Star GraphNode	{
	$1->push_back($2);
	$$ = $1;
    }
;

// GraphNode GraphNode*
_O_QGraphNode_E_S_QGraphNode_E_Star_C:
    GraphNode _QGraphNode_E_Star	{
	/* no push_front
	   $2->push_front($1);
	   $$ = $2;
	   so copy the vector. */
	$$ = new ProductionVector<const POS*>();
	$$->push_back($1);
	for (std::vector<const POS*>::iterator it = $2->begin();
	     it != $2->end(); ++it)
	    $$->push_back(*it);
	$2->clear();
	delete $2;
    }
;

// ( GraphNode GraphNode* )?
_Q_O_QGraphNode_E_S_QGraphNode_E_Star_C_E_Opt:
    {
	$$ = new ProductionVector<const POS*>();
    }
    | _O_QGraphNode_E_S_QGraphNode_E_Star_C	
;

GraphNode:
    VarOrTerm	
  | TriplesNode	
;

VarOrTerm:
    Var	{
	$$ = $1;
    }
  | GraphTerm	
;

VarOrIRIref:
    Var	{
	$$ = $1;
    }
  | IRIref	{
	$$ = $1;
    }
;

Var:
    VAR1	
  | VAR2	
;

GraphTerm:
    IRIref	{
	$$ = $1;
    }
  | RDFLiteral	{
	$$ = $1;
    }
  | NumericLiteral	{
	$$ = $1;
    }
  | BooleanLiteral	{
	$$ = $1;
    }
  | BlankNode	
  | NIL	{
	$$ = driver.getURI("http://www.w3.org/1999/02/22-rdf-syntax-ns#nil"); // !!! new GraphTerm_rule5($1);
    }
;

Expression:
    ConditionalOrExpression	
;

ConditionalOrExpression:
    // ConditionalAndExpression ( '||' ConditionalAndExpression )*
    ConditionalAndExpression _Q_O_QGT_OR_E_S_QConditionalAndExpression_E_C_E_Star	{
	$$ = $2->size() > 0 ? new BooleanDisjunction($1, $2) : $1;
	$2->clear();
	delete $2;
    }
;

// '||' ConditionalAndExpression
_O_QGT_OR_E_S_QConditionalAndExpression_E_C:
    GT_OR ConditionalAndExpression	{
	$$ = $2;
    }
;

// ( '||' ConditionalAndExpression )*
_Q_O_QGT_OR_E_S_QConditionalAndExpression_E_C_E_Star:
    {
	$$ = new ProductionVector<const Expression*>();
    }
  | _Q_O_QGT_OR_E_S_QConditionalAndExpression_E_C_E_Star _O_QGT_OR_E_S_QConditionalAndExpression_E_C	{
	$1->push_back($2);
	$$ = $1;
    }
;

ConditionalAndExpression:
    // ValueLogical ( '&&' ValueLogical )*
    ValueLogical _Q_O_QGT_AND_E_S_QValueLogical_E_C_E_Star	{
	$$ = $2->size() > 0 ? new BooleanConjunction($1, $2) : $1;
	$2->clear();
	delete $2;
    }
;

// '&&' ValueLogical
_O_QGT_AND_E_S_QValueLogical_E_C:
    GT_AND ValueLogical	{
	$$ = $2;
    }
;

// ( '&&' ValueLogical )*
_Q_O_QGT_AND_E_S_QValueLogical_E_C_E_Star:
    {
	$$ = new ProductionVector<const Expression*>();
    }
  | _Q_O_QGT_AND_E_S_QValueLogical_E_C_E_Star _O_QGT_AND_E_S_QValueLogical_E_C	{
	$1->push_back($2);
	$$ = $1;
    }
;

ValueLogical:
    RelationalExpression	
;

RelationalExpression:
    // NumericExpression ( '=' NumericExpression | '!=' NumericExpression | '<' NumericExpression | '>' NumericExpression | '<=' NumericExpression | '>=' NumericExpression )?
    NumericExpression _Q_O_QGT_EQUAL_E_S_QNumericExpression_E_Or_QGT_NEQUAL_E_S_QNumericExpression_E_Or_QGT_LT_E_S_QNumericExpression_E_Or_QGT_GT_E_S_QNumericExpression_E_Or_QGT_LE_E_S_QNumericExpression_E_Or_QGT_GE_E_S_QNumericExpression_E_C_E_Opt	{
	if ($2) {
	    $2->setLeftParm($1);
	    $$ = new ComparatorExpression($2); // !!!
	} else
	    $$ = $1;
    }
;

// '=' NumericExpression | '!=' NumericExpression | '<' NumericExpression | '>' NumericExpression | '<=' NumericExpression | '>=' NumericExpression
_O_QGT_EQUAL_E_S_QNumericExpression_E_Or_QGT_NEQUAL_E_S_QNumericExpression_E_Or_QGT_LT_E_S_QNumericExpression_E_Or_QGT_GT_E_S_QNumericExpression_E_Or_QGT_LE_E_S_QNumericExpression_E_Or_QGT_GE_E_S_QNumericExpression_E_C:
    GT_EQUAL NumericExpression	{
	$$ = new BooleanEQ($2);
    }
  | GT_NEQUAL NumericExpression	{
	$$ = new BooleanNE($2);
    }
  | GT_LT NumericExpression	{
	$$ = new BooleanLT($2);
    }
  | GT_GT NumericExpression	{
	$$ = new BooleanGT($2);
    }
  | GT_LE NumericExpression	{
    $$ = new BooleanLE($2);
    }
  | GT_GE NumericExpression	{
	$$ = new BooleanGE($2);
    }
;

// ( '=' NumericExpression | '!=' NumericExpression | '<' NumericExpression | '>' NumericExpression | '<=' NumericExpression | '>=' NumericExpression )?
_Q_O_QGT_EQUAL_E_S_QNumericExpression_E_Or_QGT_NEQUAL_E_S_QNumericExpression_E_Or_QGT_LT_E_S_QNumericExpression_E_Or_QGT_GT_E_S_QNumericExpression_E_Or_QGT_LE_E_S_QNumericExpression_E_Or_QGT_GE_E_S_QNumericExpression_E_C_E_Opt:
    {
	$$ = NULL;
    }

  | _O_QGT_EQUAL_E_S_QNumericExpression_E_Or_QGT_NEQUAL_E_S_QNumericExpression_E_Or_QGT_LT_E_S_QNumericExpression_E_Or_QGT_GT_E_S_QNumericExpression_E_Or_QGT_LE_E_S_QNumericExpression_E_Or_QGT_GE_E_S_QNumericExpression_E_C	
;

NumericExpression:
    AdditiveExpression	
;

AdditiveExpression:
    // MultiplicativeExpression ( '+' MultiplicativeExpression | '-' MultiplicativeExpression | NumericLiteralPositive | NumericLiteralNegative )*
    MultiplicativeExpression _Q_O_QGT_PLUS_E_S_QMultiplicativeExpression_E_Or_QGT_MINUS_E_S_QMultiplicativeExpression_E_Or_QNumericLiteralPositive_E_Or_QNumericLiteralNegative_E_C_E_Star	{
	$$ = $2->size() > 0 ? new ArithmeticSum($1, $2) : $1;
	$2->clear();
	delete $2;
    }
;

// '+' MultiplicativeExpression | '-' MultiplicativeExpression | NumericLiteralPositive | NumericLiteralNegative
_O_QGT_PLUS_E_S_QMultiplicativeExpression_E_Or_QGT_MINUS_E_S_QMultiplicativeExpression_E_Or_QNumericLiteralPositive_E_Or_QNumericLiteralNegative_E_C:
    GT_PLUS MultiplicativeExpression	{
	$$ = $2;
    }
  | GT_MINUS MultiplicativeExpression	{
	$$ = new ArithmeticNegation($2);
    }
  | NumericLiteralPositive	{
	$$ = new NumberExpression($1);
    }
  | NumericLiteralNegative	{
	$$ = new NumberExpression($1);
    }
;

// ( '+' MultiplicativeExpression | '-' MultiplicativeExpression | NumericLiteralPositive | NumericLiteralNegative )*
_Q_O_QGT_PLUS_E_S_QMultiplicativeExpression_E_Or_QGT_MINUS_E_S_QMultiplicativeExpression_E_Or_QNumericLiteralPositive_E_Or_QNumericLiteralNegative_E_C_E_Star:
    {
	$$ = new ProductionVector<const Expression*>();
    }
  | _Q_O_QGT_PLUS_E_S_QMultiplicativeExpression_E_Or_QGT_MINUS_E_S_QMultiplicativeExpression_E_Or_QNumericLiteralPositive_E_Or_QNumericLiteralNegative_E_C_E_Star _O_QGT_PLUS_E_S_QMultiplicativeExpression_E_Or_QGT_MINUS_E_S_QMultiplicativeExpression_E_Or_QNumericLiteralPositive_E_Or_QNumericLiteralNegative_E_C	{
	$1->push_back($2);
	$$ = $1;
    }
;

MultiplicativeExpression:
    // UnaryExpression ( '*' UnaryExpression | '/' UnaryExpression )*
    UnaryExpression _Q_O_QGT_TIMES_E_S_QUnaryExpression_E_Or_QGT_DIVIDE_E_S_QUnaryExpression_E_C_E_Star	{
	$$ = $2->size() > 0 ? new ArithmeticProduct($1, $2) : $1;
	$2->clear();
	delete $2;
    }
;

// '*' UnaryExpression | '/' UnaryExpression
_O_QGT_TIMES_E_S_QUnaryExpression_E_Or_QGT_DIVIDE_E_S_QUnaryExpression_E_C:
    GT_TIMES UnaryExpression	{
	$$ = $2;
    }
  | GT_DIVIDE UnaryExpression	{
	$$ = new ArithmeticInverse($2);
    }
;

// ( '*' UnaryExpression | '/' UnaryExpression )*
_Q_O_QGT_TIMES_E_S_QUnaryExpression_E_Or_QGT_DIVIDE_E_S_QUnaryExpression_E_C_E_Star:
    {
	$$ = new ProductionVector<const Expression*>();
    }
  | _Q_O_QGT_TIMES_E_S_QUnaryExpression_E_Or_QGT_DIVIDE_E_S_QUnaryExpression_E_C_E_Star _O_QGT_TIMES_E_S_QUnaryExpression_E_Or_QGT_DIVIDE_E_S_QUnaryExpression_E_C	{
	$1->push_back($2);
	$$ = $1;
    }
;

UnaryExpression:
    GT_NOT PrimaryExpression	{
	$$ = new BooleanNegation($2);
    }
  | GT_PLUS PrimaryExpression	{
	$$ = $2;
    }
  | GT_MINUS PrimaryExpression	{
	$$ = new ArithmeticNegation($2);
    }
  | PrimaryExpression	
;

PrimaryExpression:
    BrackettedExpression	
  | BuiltInCall	
  | IRIrefOrFunction	
  | RDFLiteral	{
	$$ = new LiteralExpression($1);
    }
  | NumericLiteral	{
	$$ = new NumberExpression($1);
    }
  | BooleanLiteral	{
	$$ = new BooleanExpression($1);
    }
  | Var	{
	$$ = new VarExpression($1);
    }
;

BrackettedExpression:
    GT_LPAREN Expression GT_RPAREN	{
	$$ = $2;
    }
;

BuiltInCall:
    IT_STR GT_LPAREN Expression GT_RPAREN	{
	$$ = new FunctionCallExpression(new FunctionCall(driver.getURI("http://www.w3.org/TR/rdf-sparql-query/#func-str"), $3, NULL, NULL));
    }
  | IT_LANG GT_LPAREN Expression GT_RPAREN	{
	$$ = new FunctionCallExpression(new FunctionCall(driver.getURI("http://www.w3.org/TR/rdf-sparql-query/#func-lang"), $3, NULL, NULL));
    }
  | IT_LANGMATCHES GT_LPAREN Expression GT_COMMA Expression GT_RPAREN	{
	$$ = new FunctionCallExpression(new FunctionCall(driver.getURI("http://www.w3.org/TR/rdf-sparql-query/#func-langMatches"), $3, $5, NULL));
    }
  | IT_DATATYPE GT_LPAREN Expression GT_RPAREN	{
	$$ = new FunctionCallExpression(new FunctionCall(driver.getURI("http://www.w3.org/TR/rdf-sparql-query/#func-datatype"), $3, NULL, NULL));
    }
  | IT_BOUND GT_LPAREN Var GT_RPAREN	{
	$$ = new FunctionCallExpression(new FunctionCall(driver.getURI("http://www.w3.org/TR/rdf-sparql-query/#func-bound"), new VarExpression($3), NULL, NULL));
    }
  | IT_sameTerm GT_LPAREN Expression GT_COMMA Expression GT_RPAREN	{
	$$ = new FunctionCallExpression(new FunctionCall(driver.getURI("http://www.w3.org/TR/rdf-sparql-query/#func-sameTerm"), $3, $5, NULL));
    }
  | IT_isIRI GT_LPAREN Expression GT_RPAREN	{
	$$ = new FunctionCallExpression(new FunctionCall(driver.getURI("http://www.w3.org/TR/rdf-sparql-query/#func-isIRI"), $3, NULL, NULL));
    }
  | IT_isURI GT_LPAREN Expression GT_RPAREN	{
	$$ = new FunctionCallExpression(new FunctionCall(driver.getURI("http://www.w3.org/TR/rdf-sparql-query/#func-isIRI"), $3, NULL, NULL));
    }
  | IT_isBLANK GT_LPAREN Expression GT_RPAREN	{
	$$ = new FunctionCallExpression(new FunctionCall(driver.getURI("http://www.w3.org/TR/rdf-sparql-query/#func-isBlank"), $3, NULL, NULL));
    }
  | IT_isLITERAL GT_LPAREN Expression GT_RPAREN	{
	$$ = new FunctionCallExpression(new FunctionCall(driver.getURI("http://www.w3.org/TR/rdf-sparql-query/#func-isLiteral"), $3, NULL, NULL));
    }
  | RegexExpression	
;

RegexExpression:
    // 'REGEX' '(' Expression ',' Expression ( ',' Expression )? ')'
    IT_REGEX GT_LPAREN Expression GT_COMMA Expression _Q_O_QGT_COMMA_E_S_QExpression_E_C_E_Opt GT_RPAREN	{
	$$ = new FunctionCallExpression(new FunctionCall(driver.getURI("http://www.w3.org/TR/rdf-sparql-query/#func-regex"), $3, $5, $6));
    }
;

// ( ',' Expression )?
_Q_O_QGT_COMMA_E_S_QExpression_E_C_E_Opt:
    {
	$$ = NULL;
    }
  | _O_QGT_COMMA_E_S_QExpression_E_C	
;

IRIrefOrFunction:
    IRIref _QArgList_E_Opt	{
	if ($2)
	    $$ = new FunctionCallExpression(new FunctionCall($1, $2));
	else
	    $$ = new URIExpression($1);
    }
;

// IRIref ArgList?
_QArgList_E_Opt:
    {
	$$ = NULL;
    }
  | ArgList	
;

RDFLiteral:
    // String ( LANGTAG | ( '^^' IRIref ) )?
    String _Q_O_QLANGTAG_E_Or_QGT_DTYPE_E_S_QIRIref_E_C_E_Opt	{
	$$ = driver.getRDFLiteral(*$1, $2.uri, $2.langtag);
	delete $1;
    }
;

// '^^' IRIref
_O_QGT_DTYPE_E_S_QIRIref_E_C:
    GT_DTYPE IRIref	{
	$$ = $2;
    }
;

// LANGTAG | ( '^^' IRIref )
_O_QLANGTAG_E_Or_QGT_DTYPE_E_S_QIRIref_E_C:
    LANGTAG	{
	$$.uri = NULL;
	$$.langtag = $1;
    }
  | _O_QGT_DTYPE_E_S_QIRIref_E_C	{
	$$.uri = $1;
	$$.langtag = NULL;
    }
;

// ( LANGTAG | ( '^^' IRIref ) )?
_Q_O_QLANGTAG_E_Or_QGT_DTYPE_E_S_QIRIref_E_C_E_Opt:
    {
	$$.uri = NULL;
	$$.langtag = NULL;
    }
  | _O_QLANGTAG_E_Or_QGT_DTYPE_E_S_QIRIref_E_C	
;

NumericLiteral:
    NumericLiteralUnsigned	
  | NumericLiteralPositive	
  | NumericLiteralNegative	
;

NumericLiteralUnsigned:
    INTEGER	
  | DECIMAL	
  | DOUBLE	
;

NumericLiteralPositive:
    INTEGER_POSITIVE	
  | DECIMAL_POSITIVE	
  | DOUBLE_POSITIVE	
;

NumericLiteralNegative:
    INTEGER_NEGATIVE	
  | DECIMAL_NEGATIVE	
  | DOUBLE_NEGATIVE	
;

BooleanLiteral:
    IT_true	
  | IT_false	
    ;

String:
    STRING_LITERAL1	
  | STRING_LITERAL2	
  | STRING_LITERAL_LONG1	
  | STRING_LITERAL_LONG2	
;

IRIref:
    IRI_REF	
  | PrefixedName	
;

PrefixedName:
    PNAME_LN	
  | PNAME_NS	
;

BlankNode:
    BLANK_NODE_LABEL	
  | ANON	
;

/*** END SPARQLfed - Change the grammar rules above ***/

%% /*** Additional Code ***/

void w3c_sw::SPARQLfedParser::error(const SPARQLfedParser::location_type& l,
			    const std::string& m)
{
    driver.error(l, m);
}

/* START Driver (@@ stand-alone would allow it to be shared with other parsers */

namespace w3c_sw {

    SPARQLfedDriver::SPARQLfedDriver (std::string baseURI, POSFactory* posFactory) : 
	Driver(baseURI, posFactory), curSubject(NULL), curPredicate(NULL), 
	curBGP(NULL), neededBGP(NULL), triplesBlockOpt(NULL), 
	curBindingClause(NULL), curBinding(NULL), curExprList(NULL), 
	curGraphName(NULL), root(NULL),  unnestTree(false)
{
}

    SPARQLfedDriver::~SPARQLfedDriver ()
{
}

bool SPARQLfedDriver::parse_stream(std::istream& in, const std::string& sname)
{
    streamname = sname;

    SPARQLfedScanner scanner(this, &in);
    scanner.set_debug(trace_scanning);
    lexer = &scanner;

    SPARQLfedParser parser(*this);
    parser.set_debug_level(trace_parsing);
    // !!! Clear out namespaces!
    ignorePrefixFlag = false;
    return (parser.parse());
}

} // namespace w3c_sw

/* END Driver */


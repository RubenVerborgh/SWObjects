# q - federate a query over several independent clinical databases
# For further description, see http://spam.w3.org/2004/10/04-pharmaFederate/

# $Id: ToxicAssoc0-remote.rq,v 1.1 2009/02/17 15:15:34 eric Exp $

# Assign convenient prefixes to common for later syntactic shorthand.
PREFIX iface: <http://neuro.example/receptors#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sioc: <http://rdfs.org/sioc/ns#>
PREFIX dbp: <http://dbpedia.org/ontology/>

SELECT ?label ?post ?humanEntrez ?code ?family ?iupharNm

FROM NAMED <http://hcls.deri.org/atag/data/gabab_example.html>

WHERE {

# Get a name and a chemical from the (SQL) MicroArray database.
SERVICE <http://localhost:8888/7tm_receptors> {
  ?iuphar	iface:family	?family .
  ?iuphar	iface:code	?code .
  ?iuphar	iface:iupharName	?iupharNm .
  ?human	iface:iuphar	?iuphar .
  ?human	iface:geneName	"GABBR1" .
  ?human	iface:entrezGene	?humanEntrez .
  }
SERVICE <http://dbpedia.org/sparql?default-graph-uri=http%3A%2F%2Fdbpedia.org&should-sponge=&>
  {_:gene dbp:entrezgene ?humanEntrez ;
          rdfs:label     ?label ;
	  FILTER (lang(?label) = "en")
  }
GRAPH <http://hcls.deri.org/atag/data/gabab_example.html> {
      ?topic rdfs:label ?label .
      ?post  sioc:topic ?topic
  }
}

